// Establish version and status
ext.githubProjectName = rootProject.name // Change if github project name is not the same as the root project's name

buildscript {
    repositories {
        mavenLocal()
        mavenCentral() // maven { url 'http://jcenter.bintray.com' }
    }
    apply from: file('gradle/buildscript.gradle'), to: buildscript 
}

allprojects {
    repositories { 
        mavenCentral() // maven { url: 'http://jcenter.bintray.com' }
    }
}

apply plugin: 'idea'
apply plugin: 'osgi'

apply from: file('gradle/convention.gradle')
apply from: file('gradle/maven.gradle')
//apply from: file('gradle/check.gradle')
//apply from: file('gradle/license.gradle')
apply from: file('gradle/release.gradle')
apply from: file('gradle/dependency-versions.gradle')

subprojects {
    group = "com.netflix.${githubProjectName}"

    apply plugin: 'eclipse'
    apply plugin: 'idea'

    dependencies {
        compile "joda-time:joda-time:$jodaTimeVersion"
        compile "com.github.stephenc.high-scale-lib:high-scale-lib:$highScaleLibVersion"
        compile ("com.google.guava:guava:$guavaVersion") {
             force = true
        }
        compile "com.eaio.uuid:uuid:$uuidVersion"
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        compile "org.codehaus.jettison:jettison:$jettisonVersion"
        compile "commons-codec:commons-codec:$commonsCodecVersion"
        compile "commons-lang:commons-lang:$commonsLangVersion"
        compile "com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:$concurrentLinkedHashMapVersion"
        compile "org.codehaus.jackson:jackson-core-asl:$jacksonVersion"
        compile "org.codehaus.jackson:jackson-mapper-asl:$jacksonVersion"
        compile "org.xerial.snappy:snappy-java:$snappyVersion"
        compile "org.apache.cassandra:cassandra-all:$cassandraVersion"
        testCompile "junit:junit:$junitVersion"
    }
}

project(':astyanax') {
    dependencies {
        compile  project(':astyanax-core')
        compile  project(':astyanax-cassandra')
        compile  project(':astyanax-thrift')
        compile  project(':astyanax-cql')
        compile  project(':astyanax-queue')
        compile  project(':astyanax-recipes')
        compile  project(':astyanax-entity-mapper')
        compile  project(':astyanax-examples')
        compile  project(':astyanax-contrib')
        compile  project(':astyanax-test')
    }
}

project(':astyanax-core') {
    apply plugin: 'bundle'
    apply plugin: 'java'
    dependencies {
        testCompile "junit:junit:$junitVersion"
    }
    bundle {
      instruction 'Export-Package', 'com.eaio.*,com.google.common.*,javax.management.*,org.apache.commons.lang.*,org.cliffc.high_scale_lib.*,org.joda.time.*'
      instruction 'Import-Package', '!javax.inject.*'
    }
}

project(':astyanax-thrift') {
    apply plugin: 'bundle'
    apply plugin: 'java'
    dependencies {
        compile  project(':astyanax-core')
        compile  project(':astyanax-cassandra')
        compile "org.apache.cassandra:cassandra-thrift:$cassandraVersion"
        compile "org.apache.thrift:libthrift:$thriftVersion"
    }
    bundle {
      instruction 'Export-Package', 'com.google.common.*,com.netflix.astyanax,com.netflix.astyanax.clock,com.netflix.astyanax.connectionpool,com.netflix.astyanax.connectionpool.exceptions,com.netflix.astyanax.connectionpool.impl,com.netflix.astyanax.cql,com.netflix.astyanax.ddl,com.netflix.astyanax.ddl.impl,com.netflix.astyanax.model,com.netflix.astyanax.partitioner,com.netflix.astyanax.query,com.netflix.astyanax.retry,com.netflix.astyanax.serializers,com.netflix.astyanax.shallows,com.netflix.astyanax.annotations,com.netflix.astyanax.impl,com.netflix.astyanax.util,com.eaio.*'
      instruction 'Import-Package', '!com.netflix.astyanax.test'
    }
}

project(':astyanax-cql') {
    apply plugin: 'bundle'
    apply plugin: 'java'
    dependencies {
        compile  project(':astyanax-core')
        compile  project(':astyanax-cassandra')
        compile ("com.datastax.cassandra:cassandra-driver-core:$javaDriverVersion") {
            exclude group: 'com.google.guava', module: 'guava'
        }
        compile "com.google.guava:guava:$guavaVersion"
        compile "junit:junit:$junitVersion"
    }
}

project(':astyanax-queue') {
    apply plugin: 'bundle'
    apply plugin: 'java'
    dependencies {
        compile  project(':astyanax-core')
        compile  project(':astyanax-cassandra')
        compile  project(':astyanax-thrift')
        compile  project(':astyanax-recipes')
    }
}

project(':astyanax-cassandra') {
    apply plugin: 'bundle'
    apply plugin: 'java'
    dependencies {
        compile "org.apache.cassandra:cassandra-all:$cassandraVersion"
        compile "org.apache.servicemix.bundles:org.apache.servicemix.bundles.commons-csv:$commonsCsvVersion"
        compile "io.netty:netty:$nettyVersion"   
        compile  project(':astyanax-core')
    }
    bundle {
      instruction 'Export-Package', 'com.google.common.*,com.netflix.astyanax.annotations,com.netflix.astyanax.clock,com.netflix.astyanax.connectionpool,com.netflix.astyanax.connectionpool.exceptions,com.netflix.astyanax.connectionpool.impl,com.netflix.astyanax.retry'
      instruction 'Import-Package', '!javax.inject.*'
    }
}

project(':astyanax-recipes') {
    apply plugin: 'java'
    dependencies {
        compile  project(':astyanax-core')
        compile  project(':astyanax-cassandra')
        testCompile project(':astyanax-thrift')
    }
}

project(':astyanax-entity-mapper') {
    apply plugin: 'bundle'
    apply plugin: 'java'
    dependencies {
        compile  project(':astyanax-core')
        compile  project(':astyanax-cassandra')
        compile  project(':astyanax-recipes')
    	compile  "javax.persistence:persistence-api:$jpaVersion"
        testCompile project(':astyanax-thrift')
        testCompile "junit:junit:$junitVersion"
    }
    bundle {
      instruction 'Export-Package', 'com.google.common.*,com.netflix.astyanax,com.netflix.astyanax.connectionpool,com.netflix.astyanax.connectionpool.exceptions,com.netflix.astyanax.connectionpool.impl,com.netflix.astyanax.cql,com.netflix.astyanax.ddl,com.netflix.astyanax.impl,com.netflix.astyanax.clock,com.netflix.astyanax.model,com.netflix.astyanax.partitioner,com.netflix.astyanax.query,com.netflix.astyanax.recipes.reader,com.netflix.astyanax.retry,com.netflix.astyanax.serializers,com.netflix.astyanax.annotations,com.netflix.astyanax.shallows,com.netflix.astyanax.util,com.eaio.*,com.netflix.astyanax.entitystore'
      instruction 'Import-Package', '!com.netflix.astyanax.test'
    }
}

project(':astyanax-examples') {
    apply plugin: 'java'
    dependencies {
        compile  project(':astyanax-core')
        compile  project(':astyanax-cassandra')
        compile  project(':astyanax-thrift')
        testCompile project(':astyanax-thrift')
    }
}

project(':astyanax-contrib') {
    apply plugin: 'java'
    dependencies {
        compile  project(':astyanax-cassandra')
        compile  project(':astyanax-core')
        compile 'com.netflix.eureka:eureka-client:1.1.110'
        compile 'com.netflix.archaius:archaius-core:0.5.12'
        compile project(':astyanax-thrift')
    }
}

project(':astyanax-test') {
    apply plugin: 'bundle'
    apply plugin: 'java'
    dependencies {
        compile  project(':astyanax-core')
        compile  project(':astyanax-cassandra')
        compile  project(':astyanax-thrift')
        compile  project(':astyanax-cql')
        compile  project(':astyanax-recipes')
        compile  project(':astyanax-entity-mapper')
        compile "junit:junit:$junitVersion"
    }
}
